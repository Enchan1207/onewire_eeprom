.program singlewireComm
ready:
    ; FIFOからOSRに読み込み
    pull block

    ; ピンを出力に
    set pindirs 1

    ; 下位1byte以外に値が入っている場合はリセットとみなし、飛ぶ
    out x, 24
    jmp x-- eeprom_reset

    ; そうでなければXレジスタにループ回数を代入して書き込み開始
    set x, 7

; 処理開始
send_loop:
    ; yレジスタにOSRをシフトイン
    out y, 1
    
    ; 一定時間ピンをLにする
    jmp !y set_zero

; t_bitは20us程度にしたい、41分周で合計67cycle

; 論理「1」出力 4cy + 63cy
set_one:
    set y 30
    ; ここで1us待ちたい、4*0.3=1.2より、4cy待つ
    set pins 0
    set pindirs 0 ; 入力に戻す 実はここでHに戻るまで4cy遅延する
    jmp wait_for_tbit

; 論理「0」出力 23cy + 44cy
set_zero:
    set y 21
    ; ここで7us待ちたい, 41分周なので0.3s/cy 7/0.3==23.3より、24cy待つ
    set pins 0 [19]
    set pindirs 0 ; 入力に戻す 実はここでHに戻るまで4cy遅延する

; (yレジスタの値 + 1) * 2 cy待つ
wait_for_tbit:
    jmp y-- wait_for_tbit [1]

; ループ回数をデクリメントして戻る
    set pindirs 1
    jmp x-- send_loop

; ACK/NACKを読む
    set pins 0
    jmp read_and_push_input

; リセットシーケンスを送るサブルーチン
eeprom_reset:
    ; Lにして96us待機->INにして10us待機->Lにして1~2us待機->Lが帰れば成功
    ; Lにして291cy待機->INにして30cy待機->Lにして4cy待機->Lが帰れば成功

    ; Lにセットし
    set pins 0

    ; t_reset待機 ((9 + 1) * (31 + 1) = 10 * 32 = 320cy = 96us以上)
    set x, 9
wait_for_t_reset:
    jmp x-- wait_for_t_reset [31]

    ; INに戻してt_rrt待機 (32cy, 10.49us以上)
    set pindirs 0 [31]

    ; Lにセットしてt_drr待機(1 + 4 = 5cy, 1.5us以上)
    set pindirs 1
    set pins 0 [3]
    set x, 0

; 入力を読んでスタックに返すルーチン
read_and_push_input:
    set pindirs 0
    in pins, 1
    push noblock
    jmp ready

% c-sdk {
void singlewireComm_program_init(PIO pio, uint sm, uint offset, uint pin) {

    pio_gpio_init(pio, pin);

    // 初期状態では入力にしておく
    pio_sm_set_consecutive_pindirs(pio, sm, pin, 1, false);

    pio_sm_config c = singlewireComm_program_get_default_config(offset);

    // 左シフトで入れる
    sm_config_set_in_shift(&c, false, false, 32);
    sm_config_set_out_shift(&c, false, false, 32);

    // setで設定するピンのベースと数を設定
    sm_config_set_set_pins(&c, pin, 1);

    // inで取り込むピンのベースを設定
    sm_config_set_in_pins(&c, pin);

    // クロック設定 41分周 = 3.048MHz
    sm_config_set_clkdiv(&c, 41);
    // sm_config_set_clkdiv(&c, 1250); // for debug

    pio_sm_init(pio, sm, offset, &c);
}
%}
